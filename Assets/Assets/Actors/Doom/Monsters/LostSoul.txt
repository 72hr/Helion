actor LostSoul 3006
{
    ActiveSound "skull/active"
    AttackSound "skull/melee"
    Damage 3
    DeathSound "skull/death"
    Health 100
    Height 56
    Mass 50
    Obituary "$OB_SKULL"
    PainChance 256
    PainSound "skull/pain"
    Radius 16
    RenderStyle SoulTrans
    SpawnID 110
    Speed 8
    
    MONSTER
    +DONTFALL
    +FLOAT
    +MISSILEMORE
    +NOGRAVITY
    +NOICEDEATH
    
    States
    {
    Spawn:
        SKUL AB 10 Bright A_Look
        Loop
    See:
        SKUL AB 6 Bright A_Chase
        Loop
    Missile:
        SKUL C 10 Bright A_FaceTarget
        SKUL D 4 Bright A_SkullAttack
        SKUL CD 4 Bright
        Goto Missile+2
    Pain:
        SKUL E 3 Bright
        SKUL E 3 Bright A_Pain
        Goto See
    Death:
        SKUL F 6 Bright
        SKUL G 6 Bright A_Scream
        SKUL H 6 Bright
        SKUL I 6 Bright A_NoBlocking
        SKUL J 6
        SKUL K 6
        Stop
    }
}

// For compatibility reasons...
actor BetaSkull : LostSoul 9037
{
    States
    {
    Spawn:
        SKUL A 10 A_Look
        Loop
    See:
        SKUL BCDA 5 A_Chase
        Loop
    Missile:
        SKUL E 4 A_FaceTarget
        SKUL F 5 A_BetaSkullAttack
        SKUL F 4
        Goto See
    Pain:
        SKUL G 4
        SKUL H 2 A_Pain
        Goto See
        SKUL I 4
        Goto See
    Death:
        SKUL JKLM 5
        SKUL N 5 A_Scream
        SKUL O 5
        SKUL P 5 A_Fall
        SKUL Q 5 A_Stop
        Wait
  }
}
