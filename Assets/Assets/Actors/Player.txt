actor PlayerPawn
{
    Health 100
    Height 56
    Mass 100
    PainChance 255
    Radius 16
    Speed 1
    Player.AirCapacity 1
    Player.AttackZOffset 8
    Player.ColorRange 0, 0
    Player.DamageScreenColor "FF 00 00"
    Player.FallingScreamSpeed 35, 40
    Player.FlechetteType "ArtiPoisonBag3"
    Player.ForwardMove 1, 1
    Player.GruntSpeed 12
    Player.JumpZ 8
    Player.MugShotMaxHealth 0
    Player.SideMove 1, 1
    Player.SoundClass "player"
    Player.UseRange 64
    Player.ViewHeight 41
    
    +CANPASS
    +CANPUSHWALLS
    +DROPOFF
    +FLOORCLIP
    +FRIENDLY
    +NOBLOCKMONST
    +NOTDMATCH
    +PICKUP
    +SHOOTABLE
    +SLIDESONWALLS
    +SOLID
    +TELESTOMP
    +WINDTHRUST
}

actor DoomPlayer : PlayerPawn
{
    Player.DisplayName "Marine"
    Player.StartItem "Clip", 50
    Player.StartItem "Fist"
    Player.StartItem "Pistol"
    Player.WeaponSlot 1, Fist, Chainsaw
    Player.WeaponSlot 2, Pistol
    Player.WeaponSlot 3, Shotgun, SuperShotgun
    Player.WeaponSlot 4, Chaingun
    Player.WeaponSlot 5, RocketLauncher
    Player.WeaponSlot 6, PlasmaRifle
    Player.WeaponSlot 7, BFG9000
    
    States
    {
    Spawn:
        PLAY A -1
        Loop
    See:
        PLAY ABCD 4
        Loop
    Missile:
        PLAY E 12
        Goto See
    Melee:
        PLAY F 6 Bright
        Goto Missile
    Pain:
        PLAY G 4
        PLAY G 4 A_Pain
        Goto Spawn
    Death:
        PLAY H 10
        PLAY I 10 A_PlayerScream
        PLAY J 10 A_NoBlocking
        PLAY KLM 10
        PLAY N -1
        Stop
    XDeath:
        PLAY O 5
        PLAY P 5 A_XScream
        PLAY Q 5 A_NoBlocking
        PLAY RSTUV 5
        PLAY W -1
        Stop
    Raise:
        PLAY NMLKJIH 10
        Goto See
    }
}
